plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id "net.darkhax.curseforgegradle" version "1.0.7"
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
            args '--username Dev#'
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Greenhouse"
        url = 'https://maven.greenhouseteam.dev/releases/'
    }

    maven {
        name = "JitPack"
        url = 'https://jitpack.io'
        content {
            excludeGroup "io.github.edwinmindcraft"
        }
    }

    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://maven.shadowsoffire.dev/releases"
        content {
            includeGroup "dev.shadowsoffire"
        }
    }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'}
    maven { url "https://code.redspace.io/releases" }
    maven { url "https://code.redspace.io/snapshots" }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://maven.ladysnake.org/releases" }

    maven { url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven" }
    }

    maven {
        url = "https://jm.gserv.me/repository/maven-public/"
    }
    maven { url = 'https://maven.greenhouseteam.dev/releases/' }
    maven { url = 'https://maven.theillusivec4.top' }
    maven { url "https://maven.isxander.dev/releases" }
    maven { url = "https://modmaven.dev"
        content {
            excludeGroup "com.teamresourceful.resourcefulconfig"}
    }
    maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }

    mavenCentral()
    mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Origins
    implementation fg.deobf("io.github.edwinmindcraft:calio-forge:${minecraft_version}-${calio_version}")
    implementation fg.deobf("io.github.edwinmindcraft:apoli-forge:${minecraft_version}-${apoli_version}")
    implementation fg.deobf("io.github.edwinmindcraft:origins-forge:${minecraft_version}-${origins_version}")
    implementation fg.deobf("top.theillusivec4.caelus:caelus-forge:${caelus_version}")
    implementation(fg.deobf("de.dafuqs:additionalentityattributes-forge:${aea_forge_version}") {
        exclude group: 'net.minecraftforge'
        exclude group: 'com.github.llamalad.mixinextras'
    })

    // Pehkui
    implementation fg.deobf("com.github.Virtuoel:Pehkui:${pehkui_version}-${minecraft_version}-forge")

    // Mixin
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixin_extras_version}")) {
        jarJar.ranged(it, "[${mixin_extras_version},)")
    }
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Dev Env
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_id}")
    runtimeOnly fg.deobf("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}")

    // Common Shared Deps
    implementation fg.deobf("dev.isxander:yet-another-config-lib:${project.yacl_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")

    // Iron's Spells n Spellbooks
    implementation fg.deobf("io.redspace.ironsspellbooks:irons_spellbooks:${irons_version}:api")
    implementation fg.deobf("io.redspace.ironsspellbooks:irons_spellbooks:${irons_version}")
    runtimeOnly fg.deobf("software.bernie.geckolib:geckolib-forge-${geckolib_version}")
    runtimeOnly fg.deobf("dev.kosmx.player-anim:player-animation-lib-forge:${player_animator_version}")

    // Eidolon
    implementation fg.deobf("curse.maven:eidolon-repraised-870250:5877358")

    // Valoria
    implementation fg.deobf("curse.maven:valoria-698244:5947069")
    runtimeOnly fg.deobf("curse.maven:fluffy-fur-1097456:5939755")

    // Alex's
    implementation fg.deobf("curse.maven:alexs-mobs-426558:5160697")
    implementation fg.deobf("curse.maven:citadel-331936:5143956")

    // Just Leveling
    implementation fg.deobf("curse.maven:justleveling-fork-1073400:5824051")

    // Apoth
    implementation fg.deobf("dev.shadowsoffire:Placebo:${minecraft_version}-${placebo_version}")
    implementation fg.deobf("dev.shadowsoffire:ApothicAttributes:${minecraft_version}-${attributeslib_version}")
    implementation fg.deobf("dev.shadowsoffire:Apotheosis:${minecraft_version}-${apotheosis_version}")

    // Medieval
    implementation fg.deobf("curse.maven:medieval-core-mod-1055023:5753791")
    runtimeOnly fg.deobf("curse.maven:ydms-mobhealthbar-mod-559894:4818965")

    // Legendary Survival Overhaul
    implementation fg.deobf("curse.maven:legendary-survival-overhaul-840254:5921074")

    // Monobank (Thief)
    implementation fg.deobf("curse.maven:monobank-817257:5086245")

    // betta combat
    implementation fg.deobf("curse.maven:better-combat-by-daedelus-639842:5625757")

    // Backpacked
    implementation fg.deobf("curse.maven:backpacked-352835:5813547")
    runtimeOnly fg.deobf("curse.maven:framework-549225:5811593")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version         : forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors         : mod_authors, mod_description: mod_description, pack_format_number: pack_format_number,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.jarJar.configure {
    archiveClassifier = ""
    dependsOn('reobfJar')
}

reobf {
    jarJar {}
}

jar.finalizedBy('reobfJarJar')

tasks.register('sendToModpack', Copy) {
    group = 'publishing'
    dependsOn 'jarJar'
    from tasks.jarJar.archiveFile
    //wtf is a renpi
    into 'C:/Users/renpi/curseforge/minecraft/Instances/Otherworld/mods'
    rename { String fileName ->
        fileName.replace(project.version.toString(), "dev")
    }
    doLast {
        println "Mod JAR sent to modpack folder: ${tasks.jarJar.archiveFileName.get()}"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = 'publishing'
    apiToken = System.getenv("CF_TOKEN")
    def mainFile = upload(1127963, jar)
    mainFile.displayName = "v${rootProject.version}"
    mainFile.releaseType = "release"
    mainFile.changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addGameVersion("1.20.1")

    // Hard integrations
    mainFile.addRequirement("origins-forge")
    mainFile.addRequirement("pehkui")
    mainFile.addRequirement("medieval-core-mod")
    mainFile.addRequirement("justleveling-fork")
    mainFile.addRequirement("irons-spells-n-spellbooks")

    // Could maybe be made optional
    mainFile.addRequirement("legendary-survival-overhaul")
    mainFile.addRequirement("ydms-mobhealthbar-mod")
    mainFile.addRequirement("monobank")
    mainFile.addRequirement("apotheosis")
    mainFile.addRequirement("alexs-mobs")

    mainFile.addRequirement("eidolon-repraised")
    mainFile.addRequirement("valoria")
}