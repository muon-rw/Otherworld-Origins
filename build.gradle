plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "net.darkhax.curseforgegradle" version "1.0.7"
    id 'net.neoforged.moddev.legacyforge' version '2.0.126'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}


legacyForge {
    // Develop against MinecraftForge version 47.3.0 for 1.20.1 (the versions can be found at https://files.minecraftforge.net/)
    version = "1.20.1-47.4.12"
    parchment {
        minecraftVersion = "1.20.1"
        mappingsVersion = "2023.09.03"
    }
    // Validate AT files and raise errors when they have invalid targets
    // This option is false by default, but turning it on is recommended
    validateAccessTransformers = true

    runs {
        client {
            client()
            setGameDirectory(project.file('run/client'))
        }

        clientExtra {
            client()
            setGameDirectory(project.file('run/client2'))
            programArguments.addAll('--username', 'Dev2')
        }

        server {
            server()
            setGameDirectory(project.file('run/server'))
            programArgument '--nogui'
        }
        data {
            data()
        }
    }

    mods {
        testproject {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {

    maven {
        name = "Greenhouse"
        url = 'https://maven.greenhouse.lgbt/releases/'
        content {
            includeGroup "io.github.edwinmindcraft"
            includeGroup "de.dafuqs"
        }
    }

    maven { url = "https://maven.bawnorton.com/releases" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://maven.shadowsoffire.dev/releases"
        content {
            includeGroup "dev.shadowsoffire"
        }
    }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'}
    maven { url "https://code.redspace.io/releases" }
    maven { url "https://code.redspace.io/snapshots" }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://maven.ladysnake.org/releases" }

    maven { url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven" }
    }

    maven {
        url = "https://jm.gserv.me/repository/maven-public/"
    }
    maven { url = 'https://maven.theillusivec4.top' }
    maven { url "https://maven.isxander.dev/releases" }
    maven { url = "https://modmaven.dev"
        content {
            excludeGroup "com.teamresourceful.resourcefulconfig"}
    }
    maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }

    maven {
        url = "https://jitpack.io"
        content {
            includeGroup("com.github.Virtuoel")
        }
    }
    mavenCentral()
    mavenLocal()

    maven { url = "https://maven.muon.rip/releases"}
}

dependencies {

    // Origins
    modImplementation("io.github.edwinmindcraft:calio-forge:${minecraft_version}-${calio_version}")
    modImplementation("io.github.edwinmindcraft:apoli-forge:${minecraft_version}-${apoli_version}")
    modImplementation("io.github.edwinmindcraft:origins-forge:${minecraft_version}-${origins_version}")
    modImplementation("top.theillusivec4.caelus:caelus-forge:${caelus_version}")
    modImplementation("de.dafuqs:additionalentityattributes-forge:${aea_forge_version}") {
        exclude group: 'net.minecraftforge'
        exclude group: 'com.github.llamalad.mixinextras'
    }

    // Pehkui
    //modImplementation("com.github.Virtuoel:Pehkui:${pehkui_version}-${minecraft_version}-forge")

    // Mixin
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixin_squared_version}"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-forge:${mixin_squared_version}")) {
        version {
            strictly '[0.2.0, 1.0)'
            prefer '0.3.7'
        }
    }
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixin_extras_version}")) {
        version {
            strictly '[0.5.0, 1.0)'
            prefer '0.5.0'
        }
    }
    annotationProcessor ("org.spongepowered:mixin:${mixin_version}:processor")

    // Core

    modImplementation("dev.muon:otherworld-1.20.1:${otherworld_core_version}+1.20.1")
    modRuntimeOnly("curse.maven:ydms-mobhealthbar-mod-559894:4818965")

    // Dev Env
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    modRuntimeOnly("curse.maven:jade-324717:${jade_id}")
    modRuntimeOnly("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}")

    // Common Shared Deps
    modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}")
    modCompileOnly("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    modRuntimeOnly("top.theillusivec4.curios:curios-forge:${curios_version}")

    // Iron's Spells n Spellbooks
    modImplementation("io.redspace:irons_spellbooks:${irons_version}")
    modRuntimeOnly("software.bernie.geckolib:geckolib-forge-${geckolib_version}")
    modImplementation("dev.kosmx.player-anim:player-animation-lib-forge:${player_animator_version}")

    // Eidolon
    modImplementation("curse.maven:eidolon-repraised-870250:5877358")

    // Valoria
    // Aether incompat with dev environment, using old version for now
    // modImplementation("maven.modrinth:valoria:1.0.2.9")
    // modRuntimeOnly("curse.maven:tridot-1239146:7561888")
    modImplementation("maven.modrinth:valoria:0.6.4.35")
    modRuntimeOnly("curse.maven:fluffy-fur-1097456:6392791")

    // Alex's
    modImplementation("curse.maven:alexs-mobs-426558:5160697")
    modImplementation("curse.maven:citadel-331936:5143956")

    // Just Leveling
    modImplementation("curse.maven:justleveling-fork-1073400:5824051")

    // Apoth
    modImplementation("dev.shadowsoffire:Placebo:${minecraft_version}-${placebo_version}")
    modImplementation("dev.shadowsoffire:ApothicAttributes:${minecraft_version}-${attributeslib_version}")
    modImplementation("dev.shadowsoffire:Apotheosis:${minecraft_version}-${apotheosis_version}")

    // Legendary Survival Overhaul
    modImplementation("curse.maven:legendary-survival-overhaul-840254:6867014")

    // Monobank (Thief)
    modImplementation("curse.maven:thief-839159:6845226")

    // betta combat
    modImplementation("curse.maven:better-combat-by-daedelus-639842:5625757")

    // Backpacked
    modImplementation("curse.maven:backpacked-352835:5813547")
    modRuntimeOnly("curse.maven:framework-549225:5811593")

    // Shoulder Surfing
    modImplementation("curse.maven:shoulder-surfing-reloaded-243190:7392767")
    
    
    // OW CORE ONLY, CAN REMOVE AFTER JL FORK:
    // Chipped
    // modRuntimeOnly("curse.maven:chipped-456956:5813138")
    // modRuntimeOnly("curse.maven:resourceful-lib-570073:5659871")
    // modRuntimeOnly("curse.maven:stitch-841890:5176879")

    // Aether + Addons
    modImplementation("curse.maven:aether-255308:6134920")
    modImplementation("curse.maven:aether-villages-1073197:5707985")

    modCompileOnly("curse.maven:aether-lost-content-318602:4927038")
    modRuntimeOnly("curse.maven:aeroblender-879879:4789008")
    modRuntimeOnly("curse.maven:structure-gel-api-378802:5278429")
    modRuntimeOnly("curse.maven:terrablender-563928:6290448")

    modImplementation("curse.maven:ancient-aether-886751:6400544")

    // BOMD
    modRuntimeOnly("curse.maven:bosses-of-mass-destruction-forge-941573:5067728")
    modRuntimeOnly("curse.maven:cerbons-api-955605:5050080")

    // IAF
    modRuntimeOnly("curse.maven:ice-and-fire-dragons-264231:5633453")
    modRuntimeOnly("curse.maven:citadel-331936:6702068")

    // Doppel
    modRuntimeOnly("curse.maven:dark-doppelganger-1110803:6619700")

    // Autolvl
    modRuntimeOnly("curse.maven:auto-leveling-616126:4899526")

    // Traveloptics
    modImplementation("curse.maven:to-tweaks-irons-spells-1046916:7424522")
    modImplementation("curse.maven:alexs-caves-924854:5848216")
    modRuntimeOnly("curse.maven:lendercataclysm-551586:6906993")
    modRuntimeOnly("curse.maven:lionfish-api-1001614:5922047")

    // Dummy
    modRuntimeOnly("curse.maven:mmmmmmmmmmmm-225738:6860192")
    modRuntimeOnly("curse.maven:selene-499980:7191950")
    // END OW CORE DEPS
}

// Define expanded props 
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version         : forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors         : mod_authors, mod_description: mod_description, pack_format_number: pack_format_number,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}

// Create sources jar task
tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

// Create javadoc jar task
tasks.register('javadocJar', Jar) {
    from tasks.javadoc
    archiveClassifier = 'javadoc'
    dependsOn tasks.javadoc
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId = base.archivesName.get()
            // Use reobfJar as the main artifact (reobfuscated jar)
            artifact(tasks.named('reobfJar', Jar))
            // Include sources and javadoc jars
            artifact(tasks.named('sourcesJar', Jar))
            artifact(tasks.named('javadocJar', Jar))
        }
    }
    repositories {
        maven {
            name = "Muon"
            url = uri("https://maven.muon.rip/releases")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
            authentication {
                create(BasicAuthentication.class)
            }
        }
        maven {
            name = 'localMaven'
            url = uri(System.getProperty("user.home") + "/.m2/repository")
        }
    }
}

jar {
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json"
    ])
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


tasks.register('sendToModpack', Copy) {
    group = 'publishing'
    dependsOn 'reobfJar'
    from tasks.reobfJar.archiveFile
    //wtf is a renpi
    into 'C:\\Users\\renpi\\curseforge\\minecraft\\Instances\\Otherworld\\mods'
    rename { String fileName ->
        fileName.replace(project.version.toString(), "dev")
    }
    doLast {
        println "Mod JAR sent to modpack folder: ${tasks.reobfJar.archiveFileName.get()}"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = 'publishing'
    apiToken = System.getenv("CF_TOKEN")
    def mainFile = upload(1127963, reobfJar)
    mainFile.displayName = "v${rootProject.version}"
    mainFile.releaseType = "release"
    mainFile.changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addGameVersion("1.20.1")

    // Hard integrations
    mainFile.addRequirement("origins-forge")
    mainFile.addRequirement("pehkui")
    mainFile.addRequirement("otherworld-core")
    mainFile.addRequirement("justleveling-fork")
    mainFile.addRequirement("irons-spells-n-spellbooks")
    mainFile.addRequirement("altorigingui")

    // Could maybe be made optional
    mainFile.addRequirement("legendary-survival-overhaul")
    mainFile.addRequirement("ydms-mobhealthbar-mod")
    mainFile.addRequirement("thief")
    mainFile.addRequirement("apotheosis")
    mainFile.addRequirement("alexs-mobs")

    mainFile.addRequirement("valoria")
}